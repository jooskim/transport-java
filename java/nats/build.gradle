buildscript {
    ext {
        springBootVersion = '2.1.4.RELEASE'
    }
    repositories {
        maven { url "http://build-squid.eng.vmware.com/nexus/content/groups/repo" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.com.github.onslip:gradle-one-jar:1.0.5"
    }
}

// Configure latest Lombok
plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
}
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'com.github.onslip.gradle-one-jar'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
apply plugin: 'jacoco'

def profiles = 'prod'

group = 'com.vmware.bifrost'
sourceCompatibility = 1.8

initProperties()

ext.bifrost_version = "[0.0.34,)"
ext.generatedResourcesOutputDir = file("$buildDir/resources")

task generateBifrostVersionPropertiesFile {
    ext.outputFile = file("$generatedResourcesOutputDir/main/version.properties")
    outputs.file(outputFile)
    doLast {
        outputFile.text = "bifrost-nats.version=$version"
    }
}

sourceSets {
    main {
        // Register the generated-resources dir on the main sourceSet.
        output.dir generatedResourcesOutputDir, builtBy: generateBifrostVersionPropertiesFile
    }
}

jar {
    enabled = true

    // Exclude application, samples and oldsamples resources and classes
    from sourceSets.main.output
    include 'com/vmware/bifrost/**/*'

    manifest {
        attributes (
                'Bundle-Name': 'Bifrost-Nats',
                'Bundle-Version': version,
                'Bundle-SymbolicName': 'com.vmware.bifrost.nats',
                'Import-Package': 'org.springframework.beans.factory,' +
                        'org.slf4j,' +
                        'com.vmware.bifrost.broker,',
                'Export-Package': "com.vmware.bifrost.broker.nats;version=$version"
        )
    }
}

repositories {
    maven { url "http://build-squid.eng.vmware.com/nexus/content/groups/repo" }
    maven { url "http://build-artifactory.eng.vmware.com/artifactory/bifrost-maven-local" }
}

// Maven artifact publishing using the maven-publish plugin.
publishing {
    publications {
        // Publishes
        mavenJava(MavenPublication) {
            // Publishes a POM to maven repo.
            from components.java
        }
    }

    repositories {
        maven {
            // To publish to the build-artifactory:
            //
            // 1. Ensure you have write access to the bifrost-maven-local repo.
            // 2. Update the version in "version.txt" file.
            // 3. Run "gradlew publish" task by specifying your credentials using the
            //    mavenUser and mavenPassword gradle properties. This can be done via:
            //       - command line arguments - "gradlew publish -PmavenUser=user -PmavenPassword=password"
            //       - the local gradle.properties file, usually located in USER_HOME/.gradle/gradle.properties
            //
            // To test you changes during development, you can publish project's jars
            // to your maven local repo by running "gradlew publishToMavenLocal" task.
            credentials {
                username mavenUser
                password mavenPassword
            }
            name = 'build-artifactory'
            url = "https://build-artifactory.eng.vmware.com/artifactory/bifrost-maven-local"
        }
    }
}

dependencies {
    implementation "com.vmware.bifrost:vmw-bifrost:$bifrost_version"
    runtime "com.vmware.bifrost:vmw-bifrost:$bifrost_version"

    implementation "io.nats:jnats:2.6.6"
    implementation group: 'commons-configuration', name: 'commons-configuration', version: '1.7'
    runtime group: 'commons-configuration', name: 'commons-configuration', version: '1.7'

    testImplementation 'org.mockito:mockito-core:2.+'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.4'
    testImplementation("junit:junit")
}

jacoco {
    toolVersion = "0.8.0"
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['natsApp/**'])
        }))
    }
}

void initProperties() {
    version = rootProject.file('version.txt').getText('UTF-8').trim()
    if ("snapshot".compareTo(buildType) == 0) {
        version = version + "-SNAPSHOT"
    } else if ("release".compareTo(buildType) == 0) {
        version = version + "-RELEASE"
    }

    if (System.getenv("bifrost_maven_user")?.trim()) {
        mavenUser = System.getenv("bifrost_maven_user").trim()
    }

    if (System.getenv("bifrost_maven_password")?.trim()) {
        mavenPassword = System.getenv("bifrost_maven_password").trim()
    }
}
