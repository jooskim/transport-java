buildscript {
    ext {
        springBootVersion = '2.0.0.M7'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.8'
}


apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'
apply plugin: 'jacoco'

jar {
    enabled = true
}

bootJar {
    classifier = 'boot'
    manifest {
        attributes(
                'Main-Class': 'oldsamples.Application'
        )
    }
}

def profiles = 'prod'

group = 'com.vmware.bifrost'
sourceCompatibility = 1.8

initProperties()

// Helper task that prints calculated project properties. Can be used for debug
// purposes.
//noinspection GroovyAssignabilityCheck
task printDebugProps {
    doLast {
        println 'version: ' + version
        println 'buildType: ' + buildType
        println 'mavenUser: ' + mavenUser
        println 'mavenPassword: ' + mavenPassword
    }
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    jcenter()
}

// Maven artifact publishing using the maven-publish plugin.
publishing {
    publications {
        // Publishes
        mavenJava(MavenPublication) {
            // Publishes a POM to maven repo.
            from components.java
        }
    }

    repositories {
        maven {
            // To publish to the build-artifactory:
            //
            // 1. Ensure you have write access to the bifrost-maven-local repo.
            // 2. Update the version in "version.txt" file.
            // 3. Run "gradlew publish" task by specifying your credentials using the
            //    mavenUser and mavenPassword gradle properties. This can be done via:
            //       - command line arguments - "gradlew publish -PmavenUser=user -PmavenPassword=password"
            //       - the local gradle.properties file, usually located in USER_HOME/.gradle/gradle.properties
            //
            // To test you changes during development, you can publish project's jars
            // to your maven local repo by running "gradlew publishToMavenLocal" task.
            credentials {
                username mavenUser
                password mavenPassword
            }
            name = 'build-artifactory'
            url = "https://build-artifactory.eng.vmware.com/artifactory/bifrost-maven-local"
        }
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-webflux')
    compile('org.springframework.boot:spring-boot-starter-websocket')
    compile('org.springframework.security:spring-security-web:5.0.0.RELEASE')
    compile('org.springframework.security:spring-security-config:5.0.0.RELEASE')
    compile('org.springframework.security:spring-security-test:5.0.0.RELEASE')
    testCompile('com.github.tomakehurst:wiremock:2.19.0')


    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('io.projectreactor:reactor-test')

    testCompile("junit:junit")
    runtime "io.reactivex.rxjava2:rxjava:2.1.9"
    compile "io.reactivex.rxjava2:rxjava:2.1.9"
    compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.9.4"
    compile "com.fasterxml.jackson.module:jackson-module-jsonSchema:2.9.4"
    compile group: "org.slf4j", name: "slf4j-api", version: "1.7.25"
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'joda-time:joda-time:2.9.9'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    testCompile group: 'org.springframework', name: 'spring-web', version: '5.0.3.RELEASE'
    testCompile 'org.mockito:mockito-core:2.+'



}

test {
    testLogging {
        showStandardStreams = true
        afterSuite { desc, result ->
            if (!desc.parent) { // true after all suites have executed and final result available.
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.0"
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
    }
}

void initProperties() {
    version = rootProject.file('version.txt').getText('UTF-8').trim()
    if ("snapshot".compareTo(buildType) == 0) {
        version = version + "-SNAPSHOT"
    }

    if (System.getenv("bifrost_maven_user")?.trim()) {
        mavenUser = System.getenv("bifrost_maven_user").trim()
    }

    if (System.getenv("bifrost_maven_password")?.trim()) {
        mavenPassword = System.getenv("bifrost_maven_password").trim()
    }
}